apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A libGDX extension for easier and faster game development'
                    url = 'https://github.com/isoteriktechnologies/racken'

                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/isoteriktechnologies/racken/blob/main/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'isoteriksoftware'
                            name = 'Imran Abdulmalik'
                            email = 'imranabdulmalik01@gmail.com'
                        }
                        developer {
                            id = 'iysheme'
                            name = 'Ibrahim Yakubu Sheme'
                            email = 'iysheme@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/isoteriktechnologies/racken.git'
                        developerConnection = 'scm:git:ssh://github.com/isoteriktechnologies/racken.git'
                        url = 'https://github.com/isoteriktechnologies/racken/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}